INCLUDE ~%MOD_FOLDER%/lib/remapped_tis.tpa~

ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_anim_at_night BEGIN // to remove distracting animations of the maps
		AR1200TN => BG1200
		AR900WN1 => BG0900
		AR900WN2 => BG0900
		AR900WN4 => BG0900
		AR900WD1 => BG0900
END
ACTION_IF GAME_IS ~tutu_totsc~ BEGIN
	ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_at_night BEGIN // turn off sleeping animals at night
		_bird => 1
		_eagle => 1
		_vultur => 1
		_seagul => 1
	END
END ELSE BEGIN
	ACTION_DEFINE_ASSOCIATIVE_ARRAY turn_off_at_night BEGIN // turn off sleeping animals at night
		bird => 1
		eagle => 1
		vultur => 1
		seagul => 1
	END
END

DEFINE_ACTION_FUNCTION ~SRHTLMLN~	// add to the bmp files
	STR_VAR
		type = ~~
BEGIN
	ACTION_IF ~%type%~ STR_EQ ~LN~ BEGIN
		OUTER_SPRINT folder ~%ee%~
	END ELSE BEGIN
		OUTER_SPRINT folder ~~
	END
	LAF ALTER_SEARCHMAP
		STR_VAR
			path_to_2da_file 	= EVAL ~%MOD_FOLDER%/base/%folder%/bmp%type%/%are%%type%.2da~ 
			areaname 			= EVAL ~%are2%~ // area name, e.g. ~AR3700~
			output_path			= EVAL ~%biffdir%~ // output directory
			type				= EVAL ~%type%~
	END
END

DEFINE_ACTION_FUNCTION ~BACKUP_BIFFING~ // for the uninstallation of the mod
	STR_VAR
		ext = ~~
BEGIN
	ACTION_IF FILE_EXISTS ~override/%are2%%ext%~ BEGIN
		MOVE ~override/%are2%%ext%~ ~%MOD_FOLDER%/backup_ovr~
	END
END


DEFINE_PATCH_MACRO ~RENAME_OVL_NAMES~ // replace ovl tis names automatically
BEGIN
	READ_LONG 0x08 ovl_num
	READ_LONG 0x10 ovl_off
	FOR (cnt=0; cnt<ovl_num; cnt+=1) BEGIN
		READ_ASCII ovl_off+cnt*0x18+0x04 tis_name
		PATCH_IF ~%tis_name%~ STR_EQ ~%are2%~ BEGIN
			WRITE_ASCIIE ovl_off+cnt*0x18+0x04 ~%are2%N~
		END
		INNER_PATCH_SAVE new_tis_name ~%tis_name%~ BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~WT~ ~YS~
		END
		WRITE_ASCIIE ovl_off+cnt*0x18+0x04 ~%new_tis_name%~
	END
END

DEFINE_PATCH_MACRO ~CLEAR_ARRAY~
BEGIN
	CLEAR_ARRAY x_coord
	CLEAR_ARRAY y_coord
END

DEFINE_PATCH_MACRO ~DELETE_EXISTING_ANIMATIONS~ // to delete torches, candles ...
BEGIN
	GET_OFFSET_ARRAY animation_array ARE_V10_ANIMATIONS
	PATCH_PHP_EACH animation_array AS animation_num => animation_offset BEGIN
		READ_SHORT animation_offset+0x20 animation_pos_x
		READ_SHORT animation_offset+0x22 animation_pos_y
		PATCH_IF ((~%animation_pos_x%~ = $x_coord("%index%")) AND (~%animation_pos_y%~ =  $y_coord("%index%"))) BEGIN
			LPF fj_are_structure INT_VAR fj_delete_mode = animation_num STR_VAR fj_structure_type = animation END
		END
	END
END

DEFINE_ACTION_FUNCTION ~INSTALL_TILESETS~
	INT_VAR
		debug = 0
	STR_VAR
		purple = ~~
		file_list = EVAL ~%file_list%~ 
		wed_list = EVAL  ~%wed_list%~
	RET file_list
		wed_list
BEGIN
	
	ACTION_IF (VARIABLE_IS_SET $EVAL ~remapped_tis~(~%are%~) OR FILE_EXISTS_IN_GAME ~%are%.are~) BEGIN
		ACTION_IF (~%purple%~ STR_EQ ~BG1~) BEGIN
			OUTER_TEXT_SPRINT are2 $EVAL ~remapped_tis~(~%are%~)
		END
		ACTION_IF (~%purple%~ STR_EQ ~TOB~) BEGIN
			OUTER_TEXT_SPRINT are2 ~%are%~
		END
		ACTION_IF (~%purple%~ STR_EQ ~SOD~) BEGIN
			OUTER_TEXT_SPRINT are2 ~%are%~
		END
		ACTION_IF (~%purple%~ STR_EQ ~BG2EE~) BEGIN
			OUTER_TEXT_SPRINT are2 ~%are%~
		END
		ACTION_IF (~%purple%~ STR_EQ ~BG1EE~) BEGIN
			OUTER_TEXT_SPRINT are2 ~%are%~
		END
		
		ACTION_IF debug BEGIN
			OUTER_SPRINT file_list ~%file_list%%LNL%%seperator%%LNL%%are%%LNL%~
		END
		ACTION_IF FILE_EXISTS_IN_GAME ~%are2%.are~ BEGIN
		
			// Extended night & creatures sleep at night
			COPY_EXISTING ~%are2%.are~ ~override~
			PATCH_IF SOURCE_SIZE > 0x11b BEGIN // Valid size check
				PATCH_IF (((BYTE_AT 0x48) << 7) & BIT7) BEGIN // Toggle only outdoor areas
					WRITE_BYTE 0x48 (THIS | BIT6 | BIT1 | BIT2) // Toggle extended night flag (BIT6) and day/night flag (BIT1) and weather flag (BIT2)
					GET_OFFSET_ARRAY actors 0x54 4 0x58 2 0 0 0x110		// turn off creatures at night time
					PHP_EACH actors AS i => off BEGIN
						READ_ASCII off + 0x80 cre_resref
						TO_LOWER cre_resref
						PATCH_IF VARIABLE_IS_SET $turn_off_at_night("%cre_resref%") BEGIN
							WRITE_LONG off + 0x40 0x1ffe0 // 05:30 to 17:29
						END
					END
					GET_OFFSET_ARRAY animations 0xb0 4 0xac 4 0 0 0x4c	// turn off animations at night time
					PHP_EACH animations AS i => off BEGIN
						READ_ASCII off + 0x0028 animation
						TO_UPPER animation
						PATCH_IF VARIABLE_IS_SET $turn_off_anim_at_night("%animation%") BEGIN
							WRITE_LONG off+0x34 0x00
						END
						
					END
				END
				PATCH_IF FILE_EXISTS ~%MOD_FOLDER%/base/are_patch/%are%_are.tpa~ BEGIN // add animation for braziers, candles ...
					PATCH_INCLUDE ~%MOD_FOLDER%/base/are_patch/%are%_are.tpa~
				END
			END
			BUT_ONLY
			
			// biff only areas with tis and pvrz files
			OUTER_SET area_biff = 0
			ACTION_IF  (FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tbc~ 
						OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ 
						OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tbc~ 
						OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~) BEGIN
				MKDIR ~%MOD_FOLDER%/bif/%are2%~
				OUTER_SPRINT biffdir ~%MOD_FOLDER%/bif/%are2%~
				OUTER_SET area_biff = 1
			END ELSE BEGIN
				OUTER_SPRINT biffdir ~%MOD_FOLDER%/bif/goMAIN~
			END
			OUTER_SPRINT temp ~%MOD_FOLDER%/bif/temp~
			
			// biff only day or night files with the tis areas
			OUTER_SET day = 0
			OUTER_SET night = 0
			
			//**************************************************
			// Day files
			//**************************************************
			
			// day tis v1 or tis v2 file 
			
			ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tbc~ OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~) BEGIN
				ACTION_IF GAME_IS ~bgt tutu_totsc tob~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tbc~ BEGIN
						LAF HANDLE_TILECONV
							STR_VAR
								input_path    = EVAL ~%MOD_FOLDER%/base/tbcD/%are%/%ee%~
								output_path	  = EVAL ~%temp%~
						END
						COPY_LARGE ~%temp%/%are%.tis~ ~%biffdir%/%are2%.tis~
						OUTER_PATCH ~~ BEGIN LPM ~DEBUG~ END
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.tis~ END
					END
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ BEGIN	// some areas have artefacts during compression, no tbc file
						COPY_LARGE ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ ~%biffdir%/%are2%.tis~
						OUTER_PATCH ~~ BEGIN LPM ~DEBUG~ END
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.tis~ END
					END
				END
				ACTION_IF GAME_IS ~eet bgee bg2ee~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ BEGIN
						COPY ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/%are%.tis~ ~%biffdir%/%are2%.tis~
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.tis~ END
						ACTION_IF (GAME_IS ~bgee~ AND (~%purple%~ STR_EQ ~BG1~)) BEGIN			// BGEE
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN
								OUTER_PATCH_SAVE newname ~%BASH_FOR_RES%~ BEGIN
									REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~B~ ~A~
								END
								COPY ~%BASH_FOR_FILESPEC%~ ~%biffdir%/%newname%.pvrz~
								LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.pvrz~ END
							END
						END ELSE BEGIN
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcD/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN		// EET, BG2EE
								COPY ~%BASH_FOR_FILESPEC%~ ~%biffdir%~
								LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.pvrz~ END
							END
						END
					END
				END
				OUTER_SET day = 1
			END
			
			// day mos file
			
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/mbcD/%ee%/%are%/%are%.mbc~ BEGIN
				LAF HANDLE_TILECONV
					STR_VAR
						input_path = EVAL ~%MOD_FOLDER%/base/mbcD/%ee%/%are%~
						output_path	  = EVAL ~%temp%~
				END
				COPY ~%temp%/%are%.mos~ ~%biffdir%/%are2%.mos~
					LPM ~DEBUG~
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.mos~ END
			END ELSE BEGIN
				ACTION_IF (area_biff = 1) AND (day = 1) BEGIN
					COPY_EXISTING ~%are2%.mos~ ~%biffdir%~
						LPM ~DEBUG~
					LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.mos~ END
				END
			END
			
			// day lightmap
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpLM/%are%LM.2da~ BEGIN
				LAF ~SRHTLMLN~ STR_VAR type = ~LM~ END
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~LM.bmp~ END
			END ELSE BEGIN
				ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpLM/%are%LM.bmp~ BEGIN
					COPY ~%MOD_FOLDER%/base/bmpLM/%are%LM.bmp~ ~%biffdir%/%are2%LM.bmp~
						LPM ~DEBUG~
					LAF ~BACKUP_BIFFING~ STR_VAR ext = ~LM.bmp~ END
					END ELSE BEGIN
					ACTION_IF (area_biff = 1) AND (day = 1) BEGIN
						COPY_EXISTING ~%are2%LM.bmp~ ~%biffdir%~
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~LM.bmp~ END
					END
				END
			END
			
			// day wed file
			
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/wedD/%ee%/%are%.wed~ BEGIN
				COPY ~%MOD_FOLDER%/base/wedD/%ee%/%are%.wed~ ~%biffdir%/%are2%.wed~
					REPLACE_TEXTUALLY ~%are%~ ~%are2%~
					PATCH_IF FILE_EXISTS ~%MOD_FOLDER%/base/wed_patch/%are%_wed.tpa~ BEGIN
						PATCH_INCLUDE ~%MOD_FOLDER%/base/wed_patch/%are%_wed.tpa~
					END
					LPM ~DEBUG~
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.wed~ END
			END ELSE BEGIN
				ACTION_IF (area_biff = 1) AND (day = 1) BEGIN
					COPY_EXISTING ~%are2%.wed~ ~%biffdir%~
						LPM ~DEBUG~
					LAF ~BACKUP_BIFFING~ STR_VAR ext = ~.wed~ END
				END
			END
			
			OUTER_SET day = 0

			//**************************************************
			// Night files
			//**************************************************
			
			ACTION_IF debug BEGIN
				OUTER_SPRINT file_list ~%file_list%%LNL%%TAB%~
			END
			
			// night tis v1 or tis v2
			
			ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tbc~ OR FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~) BEGIN
				ACTION_IF GAME_IS ~bgt tutu_totsc tob~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tbc~ BEGIN
						LAF HANDLE_TILECONV
							STR_VAR
								input_path    = EVAL ~%MOD_FOLDER%/base/tbcN/%are%/%ee%~
								output_path	  = EVAL ~%temp%~
						END
						COPY_LARGE ~%temp%/%are%N.tis~ ~%biffdir%/%are2%N.tis~
						OUTER_PATCH ~~ BEGIN LPM ~DEBUG~ END
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.tis~ END
					END
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~ BEGIN // some areas have artefacts during compression, no tbc file
						COPY_LARGE ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~ ~%biffdir%/%are2%N.tis~
						OUTER_PATCH ~~ BEGIN LPM ~DEBUG~ END
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.tis~ END
					END
				END
				ACTION_IF GAME_IS ~eet bgee bg2ee~ BEGIN
					ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~ BEGIN
						COPY ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/%are%N.tis~ ~%biffdir%/%are2%N.tis~
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.tis~ END
						ACTION_IF (GAME_IS ~bgee~ AND (~%purple%~ STR_EQ ~BG1~)) BEGIN		// BGEE 
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN
								OUTER_PATCH_SAVE newname ~%BASH_FOR_RES%~ BEGIN
									REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~B~ ~A~
								END
								COPY ~%BASH_FOR_FILESPEC%~ ~%biffdir%/%newname%.pvrz~
								LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.pvrz~ END
							END
						END ELSE BEGIN
							ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcN/%are%/%ee%/~ ~.*\.pvrz$~ BEGIN	// EET, BG2EE
								COPY ~%BASH_FOR_FILESPEC%~ ~%biffdir%~
								LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.pvrz~ END
							END
						END
					END
				END
				OUTER_SET night = 1
			END
			
			// night mos
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/mbcN/%ee%/%are%N/%are%N.mbc~ BEGIN						
				LAF HANDLE_TILECONV
					STR_VAR
						input_path    = EVAL ~%MOD_FOLDER%/base/mbcN/%ee%/%are%N~
						output_path	  = EVAL ~%temp%~
				END
				COPY ~%temp%/%are%N.mos~ ~%biffdir%/%are2%N.mos~
					LPM ~DEBUG~
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.mos~ END
			END ELSE BEGIN
				ACTION_IF (area_biff = 1) AND (night = 1) BEGIN
					ACTION_IF FILE_EXISTS_IN_GAME ~%are2%N.mos~ BEGIN
						COPY_EXISTING ~%are2%N.mos~ ~%biffdir%~
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.mos~ END
					END ELSE BEGIN
						COPY_EXISTING ~%are2%.mos~ ~%biffdir%/%are2%N.mos~
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.mos~ END
					END
				END
			END
			
			// night lightmap
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpLN/%ee%/%are%LN.2da~ BEGIN
				LAF ~SRHTLMLN~ STR_VAR type = ~LN~ END
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~LN.bmp~ END
			END ELSE BEGIN
				ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpLN/%ee%/%are%LN.bmp~ BEGIN
					LAF ~BACKUP_BIFFING~ STR_VAR ext = ~LN.bmp~ END
					COPY ~%MOD_FOLDER%/base/bmpLN/%ee%/%are%LN.bmp~ ~%biffdir%/%are2%LN.bmp~
						LPM ~DEBUG~
					END ELSE BEGIN
					ACTION_IF (area_biff = 1) AND (night = 1) BEGIN
						ACTION_IF FILE_EXISTS_IN_GAME ~%are2%LN.bmp~ BEGIN
							COPY_EXISTING ~%are2%LN.bmp~ ~%biffdir%~
								LPM ~DEBUG~
							LAF ~BACKUP_BIFFING~ STR_VAR ext = ~LN.bmp~ END
						END ELSE BEGIN
							COPY_EXISTING ~%are2%LM.bmp~ ~%biffdir%/%are2%LN.bmp~
								LPM ~DEBUG~
							LAF ~BACKUP_BIFFING~ STR_VAR ext = ~LN.bmp~ END
						END
					END
				END
			END
			
			// night wed
			
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/wedN/%ee%/%are%N.wed~ BEGIN
				COPY ~%MOD_FOLDER%/base/wedN/%ee%/%are%N.wed~ ~%biffdir%/%are2%N.wed~
					REPLACE_TEXTUALLY ~%are%~ ~%are2%~
					PATCH_IF FILE_EXISTS ~%MOD_FOLDER%/base/wed_patch/%are%_wed.tpa~ BEGIN
						PATCH_INCLUDE ~%MOD_FOLDER%/base/wed_patch/%are%_wed.tpa~
					END
					LPM ~DEBUG~
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.wed~ END
			END ELSE BEGIN
				ACTION_IF (area_biff = 1) AND (night = 1) BEGIN
					ACTION_IF FILE_EXISTS_IN_GAME ~%are2%N.wed~ BEGIN
						COPY_EXISTING + ~%are2%N.wed~ ~%biffdir%~
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.wed~ END
					END ELSE BEGIN
						COPY_EXISTING + ~%are2%.wed~ ~%biffdir%/%are2%N.wed~
							WRITE_BYTE 0x2a 0x4e // add the N to the area name 
							LPM ~RENAME_OVL_NAMES~ // change known ovl from WT to YS
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~N.wed~ END
					END
				END
			END
			
			// searchmap
			
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpSR/%are%SR.2da~ BEGIN // changes to existing SR
				LAF ~SRHTLMLN~ STR_VAR type = ~SR~ END
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~SR.bmp~ END
			END ELSE BEGIN
				ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpSR/%are%SR.bmp~ BEGIN // complete new SR
					COPY ~%MOD_FOLDER%/base/bmpSR/%are%SR.bmp~ ~%biffdir%/%are2%SR.bmp~
						LPM ~DEBUG~
					LAF ~BACKUP_BIFFING~ STR_VAR ext = ~SR.bmp~ END
					END ELSE BEGIN
					ACTION_IF (area_biff = 1)  BEGIN
						COPY_EXISTING ~%are2%SR.bmp~ ~%biffdir%~ // existing SR
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~SR.bmp~ END
					END
				END
			END
								
			// heightmap
			ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpHT/%are%HT.2da~ BEGIN
				LAF ~SRHTLMLN~ STR_VAR type = ~HT~ END
				LAF ~BACKUP_BIFFING~ STR_VAR ext = ~HT.bmp~ END
			END ELSE BEGIN
				ACTION_IF FILE_EXISTS ~%MOD_FOLDER%/base/bmpHT/%are%HT.bmp~ BEGIN
					COPY ~%MOD_FOLDER%/base/bmpHT/%are%HT.bmp~ ~%biffdir%/%are2%HT.bmp~
						LPM ~DEBUG~
					LAF ~BACKUP_BIFFING~ STR_VAR ext = ~HT.bmp~ END
					END ELSE BEGIN
					ACTION_IF (area_biff = 1) BEGIN
						COPY_EXISTING ~%are2%HT.bmp~ ~%biffdir%~
							LPM ~DEBUG~
						LAF ~BACKUP_BIFFING~ STR_VAR ext = ~HT.bmp~ END
					END
				END
			END
			
			OUTER_SET night = 0
			
			// biffing
			ACTION_IF (area_biff = 1) BEGIN
				MAKE_BIFF ~go%are2%~ BEGIN ~%biffdir%~ ~^.*$~ END
			END
			
			ACTION_IF debug BEGIN
				COPY ~.../debug_include.tpa~ ~%temp%~
				REINCLUDE ~%temp%/debug_include.tpa~
			END
			
		END
	END
END
				
DEFINE_ACTION_FUNCTION ~INSTALL_MAIN~
	INT_VAR debug = 0
BEGIN
	MKDIR ~%MOD_FOLDER%/bif~	
	MKDIR ~%MOD_FOLDER%/bif/goMAIN~
	MKDIR ~%MOD_FOLDER%/bif/temp~
	MKDIR ~%MOD_FOLDER%/backup_ovr~
	
	// tis files for water overlays
	ACTION_BASH_FOR ~%MOD_FOLDER%/base/tbcN/ys~ ~^.+\.tis$~ BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~%BASH_FOR_FILE%~ BEGIN
			COPY + ~%MOD_FOLDER%/base/tbcN/ys/%BASH_FOR_FILE%~ ~%MOD_FOLDER%/bif/goMAIN~
		END
	END
	
	// bam animations
	ACTION_BASH_FOR ~%MOD_FOLDER%/base/bam~ ~^.+\.bam$~ BEGIN
		COPY + ~%MOD_FOLDER%/base/bam/%BASH_FOR_FILE%~ ~%MOD_FOLDER%/bif/goMAIN~
	END

	//	BG1 Areas
	ACTION_IF GAME_IS ~eet bgee bgt tutu_totsc~ BEGIN
		PRINT ~BG1 Areas~
		ACTION_PHP_EACH bg1_areas AS idx => are BEGIN
			LAF ~INSTALL_TILESETS~ 
			INT_VAR debug = ~%debug%~ 
			STR_VAR purple = ~BG1~ file_list = EVAL ~%file_list%~ wed_list = EVAL ~%wed_list%~ 
			RET file_list wed_list END 
		END
	END
	
	// BG2+TOB Areas
	ACTION_IF GAME_IS ~eet bgt bg2ee tob~ BEGIN
		PRINT ~BG2 + ToB Areas~
		ACTION_PHP_EACH bg2_tob_areas AS idx => are BEGIN
			LAF ~INSTALL_TILESETS~ 
			INT_VAR debug = ~%debug%~ 
			STR_VAR purple = ~TOB~ file_list = EVAL ~%file_list%~ wed_list = EVAL ~%wed_list%~ 
			RET file_list wed_list END 
		END
	END
	
	// SoD Areas
	ACTION_IF GAME_INCLUDES ~sod~ BEGIN
		PRINT ~SoD Areas~
		ACTION_PHP_EACH sod_areas AS idx => are BEGIN
			LAF ~INSTALL_TILESETS~ 
			INT_VAR debug = ~%debug%~ 
			STR_VAR purple = ~SOD~ file_list = EVAL ~%file_list%~ wed_list = EVAL ~%wed_list%~ 
			RET file_list wed_list END
		END
	END
	
	// BG1EE Areas
	ACTION_IF GAME_IS ~eet bgee~ BEGIN
		PRINT ~BG1EE~
		ACTION_PHP_EACH bgee_areas AS idx => are BEGIN
			LAF ~INSTALL_TILESETS~ 
			INT_VAR debug = ~%debug%~ 
			STR_VAR purple = ~BG1EE~ file_list = EVAL ~%file_list%~ wed_list = EVAL ~%wed_list%~ 
			RET file_list wed_list END
		END
	END
	
	// BG2EE Areas
	ACTION_IF GAME_IS ~eet bg2ee~ BEGIN
		PRINT ~BG2EE~
		ACTION_PHP_EACH bg2ee_areas AS idx => are BEGIN
			LAF ~INSTALL_TILESETS~ 
			INT_VAR debug = ~%debug%~ 
			STR_VAR purple = ~BG2EE~ file_list = EVAL ~%file_list%~ wed_list = EVAL ~%wed_list%~ 
			RET file_list wed_list END
		END
	END
	
	MAKE_BIFF ~goMAIN~ BEGIN ~%MOD_FOLDER%/bif/goMAIN~ ~^.*$~ END
	
	// create the log files
	ACTION_IF debug BEGIN
		PRINT ~The log files debug.log and all_files.log are in the %MOD_FOLDER%/ directory~
		<<<<<<<< .../debug.log
		>>>>>>>>
		LAM ~COMPLETE_LIST_OF_FILES~
		COPY ~.../debug.log~ ~%MOD_FOLDER%/debug.log~
		APPEND_OUTER + ~%MOD_FOLDER%/debug.log~ ~%file_list%~
		APPEND_OUTER + ~%MOD_FOLDER%/debug.log~ ~%wed_list%~
	END	
	
END


